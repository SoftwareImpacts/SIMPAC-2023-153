module tools

  use global_module
  use pde 

  implicit none

  contains

  ! ####################################################################
  
  subroutine export_sol_vtk(t_ID, U)

    ! ---------------------------------------------------------------------------------------
    ! This subroutine exports data in the legacy VTK format, that can be visualized using
    ! ParaView.
    ! This helps for checking that all is good.
    ! The output file is put in the "dumps" directory and is named "sol_..." where ... is the
    ! time ID "t_ID" given as an input to the subroutine.
    ! U: conserved variables
    ! 
    ! Notes: 
    !  - All cells are printed, including the two layers of ghost cells for each boundary.
    !    This helps for debugging. 
    !  - The data is exported for each cell center. In ParaView, you may visualize the data 
    !    using the option "Surface with Edges": this is misleading, and these edges do not
    !    represent the cells boundaries. Instead, each point is a cell center.
    ! ---------------------------------------------------------------------------------------
  
    integer, intent(in) :: t_ID
    real, dimension(:,:,:), intent(in) :: U

    real, dimension(Neq,Nx,Ny) :: prim ! Primitive variables
    
    integer      :: Nx_int, Ny_int
    integer      :: ID_EQ, i, j

    character(len=512)  :: file_name  ! Name of the output file

    ! Number of internal cells
    Nx_int = Nx - 8
    Ny_int = Ny - 8

    ! ----- Compute primitive variables on the grid ------

    prim = 0.0 ! Init
    do i = 1, Nx
      do j = 1, Ny
        call CPU_compute_primitive_from_conserved(U(:,i,j), prim(:,i,j))
      end do
    end do

    ! ------- Write VTK file -------

    ! Open file, and replace if existing. 11189 is just an arbitrary ID.
    write(file_name,'(A, I8.8, A)') './dumps/sol_', t_ID, '.vtk'
    open(11189, file=file_name, status="replace", form="formatted")

    ! The VTK file starts with this header
    write(11189, '(A)') "# vtk DataFile Version 2.0"
    write(11189, '(A30,I8)') "hyper2D solution at timestep ", t_ID
    write(11189, '(A)') "ASCII"

    ! Write the information about the grid: number of points etc etc
    write(11189, '(A)') "DATASET STRUCTURED_POINTS"
    write(11189, '(A,I8,I8,I8)') "DIMENSIONS ", Nx_int, Ny_int, 1 ! Write only internal cells
    write(11189, '(A,F14.7,F14.7,F14.7)') "SPACING ", dx, dy, 0.0
    write(11189, '(A,F14.7,F14.7,F14.7)') "ORIGIN  ", x_min+dx/2.0, y_min+dy/2.0, 0.0

    ! Now write the fields as "point data", each point is a cell center.
    write(11189, '(A,I10)') "POINT_DATA ", Nx_int*Ny_int
  
    do ID_EQ = 1, Neq ! Loop over the equations
  
      ! The array "prim_names" is defined in the "pde" module
      write(11189, '(A,A,A)') "SCALARS ", prim_names(ID_EQ), " float 1"
      write(11189, '(A)') "LOOKUP_TABLE default"
  
      ! Print value (only internal cells, not ghost cells)
      do j = 5, Ny-4
        do i = 5, Nx-4
          write(11189, '(E20.7E6,A)', advance="no") prim(ID_EQ,i,j), " " ! WRITE SOL HERE
        end do
      end do
 
      write(11189,*) " "
 
    end do
  
    write(11189,*) " "

    ! write(11189,'(A)') "METADATA"
    ! write(11189,'(A)') "INFORMATION 0"
  
    close(11189)
  
  end subroutine

end module 
